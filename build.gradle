plugins {
    id "net.ltgt.apt" version "0.11"
    id "org.dm.bundle" version "0.10.0"
}

if (System.getenv("BUILD_TAG")) {
    ext.buildInfo = System.getenv("BUILD_TAG")
} else {
    ext.buildInfo = InetAddress.localHost.hostName
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    group = 'be.sailormoon.common'
    version = '1.0.0'
}

ext {
    junitVersion = '4.12'
    slf4jVersion = '1.7.5'
}

subprojects {
    apply plugin: 'org.dm.bundle'
    apply plugin: 'java'
    apply plugin: "net.ltgt.apt"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    compileJava.options.encoding = 'UTF-8'

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        testCompile "junit:junit:$junitVersion"
    }

    jar {
        manifest {
            attributes 'Build-Time': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Build-Info': project.buildInfo
        }
    }

    bundle {
        trace = false
        instruction '-plugin', 'aQute.lib.spring.SpringXMLType'
        instruction '-dsannotations', '*'
        instruction 'Bundle-Name', "SAILOR :: ${project.name.replaceFirst("-", " :: ").replaceAll("-", " ")}"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    test {
        testLogging {
            showStandardStreams = true
        }
    }
}
